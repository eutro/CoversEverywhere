buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = "${phase}-${major}.${minor}.${patch}"
group = "eutros.${mod_id}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "${mod_id}"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

sourceSets {
    common {
        java
        compileClasspath += sourceSets.main.compileClasspath
    }
    impl {
        java
        compileClasspath += sourceSets.common.compileClasspath
        compileClasspath += sourceSets.common.runtimeClasspath
    }
    modules {
        java
        compileClasspath += sourceSets.common.compileClasspath
        compileClasspath += sourceSets.common.runtimeClasspath
    }
    main {
        compileClasspath += sourceSets.common.runtimeClasspath
        runtimeClasspath += sourceSets.impl.runtimeClasspath
        runtimeClasspath += sourceSets.modules.runtimeClasspath
    }
}

jar {
    from sourceSets.main.output
    from sourceSets.api.output
    from sourceSets.common.output
    from sourceSets.impl.output
    from sourceSets.modules.output
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"

    mappings = "snapshot_20170925"

    useDepAts = true
    makeObfSourceJar = false

    if (project.hasProperty("mc_uuid")) {
        clientRunArgs += ["--uuid", project.mc_uuid]
    }

    if (project.hasProperty("mc_username")) {
        clientRunArgs += ["--username", project.mc_username]
    }

    replace "GRADLE:VERSION", project.version
}

repositories {
    maven {
        url = "https://minecraft.curseforge.com/api/maven"
    }
    maven {
        url = "http://chickenbones.net/maven/"
    }
    maven {
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url = "https://modmaven.k-4u.nl"
    }
}

dependencies {
    deobfProvided "mezz.jei:jei_1.12.2:4.15.0.293:api"
    runtime "mezz.jei:jei_1.12.2:4.15.0.293"

    deobfCompile "gregtechce:gregtech:1.12.2:1.10.5.582"

    deobfCompile "codechicken-lib-1-8:CodeChickenLib-1.12.2:3.2.3.357:universal"
}

jar {
    manifest {
        attributes 'FMLCorePluginContainsFMLMod': 'true'
        attributes 'FMLCorePlugin': 'eutros.coverseverywhere.impl.asm.CoversEverywhereCoremod'
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
